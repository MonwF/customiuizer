apply plugin: 'com.android.application'

//def acraVersion = '5.7.0'
def keystorePropertiesFile = rootProject.file("../keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
def sFile = file(keystoreProperties['storeFile'])
def sPassword = keystoreProperties['storePassword']
def kAlias = keystoreProperties['keyAlias']
def kPassword = keystoreProperties['keyPassword']
def lastVersion = 73
def lastVersionName = "24.10.12"

android {
    namespace "name.monwf.customiuizer"
    signingConfigs {
        v2 {
            storeFile sFile
            storePassword sPassword
            keyAlias kAlias
            keyPassword kPassword
            v1SigningEnabled false
            v2SigningEnabled true
        }
    }
    defaultConfig {
        applicationId "name.monwf.customiuizer"
        minSdkVersion 34
        //noinspection OldTargetApi,ExpiredTargetSdkVersion
        targetSdkVersion 34
        compileSdk 35
        versionCode lastVersion
        versionName lastVersionName
        resConfigs 'ru-rRU', 'zh-rCN', 'ja-rJP', 'vi-rVN', 'cs-rCZ', 'pt-rBR', 'tr-rTR', 'es-rES'
        ndk { abiFilters "arm64-v8a" }
    }
    buildTypes {
        develop {
            debuggable false
            minifyEnabled true
            shrinkResources true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            merges += 'META-INF/xposed/*'
            excludes += ['META-INF/*.kotlin_module', 'META-INF/androidx.*.version', '**.kotlin_builtins', '**.kotlin_metadata']
        }
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            if (variant.buildType.name == 'develop') {
                setVersionCodeOverride(variant.versionCode + 1)
                def finalVersionName = new Date().format('yy.MM.dd') + '-test'
                setVersionNameOverride(finalVersionName)
                setProperty("outputFileName", "Pengeek-" + finalVersionName + ".apk")
            }
            else {
                setProperty("outputFileName", "Pengeek-" + lastVersionName + ".apk")
            }
        }
    }
//    gradle.projectsEvaluated {
//        tasks.withType(JavaCompile) {
//            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
//        }
//    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        buildConfig = true
    }
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
}

dependencies {
    //    implementation "ch.acra:acra-core:$acraVersion"
//    noinspection DifferentStdlibGradleVersion
    compileOnly files('lib/api-100.aar')
    compileOnly files('lib/framework.jar')

    implementation "io.github.libxposed:service:100-1.0.0"
    implementation "org.apache.commons:commons-lang3:3.15.0"
    implementation "androidx.preference:preference:1.2.1"
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'org.luckypray:dexkit:2.0.2'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:2.0.0"))
//    implementation "androidx.recyclerview:recyclerview:1.2.1"
//    implementation 'com.google.android.flexbox:flexbox:3.0.0'
//    implementation 'com.google.android.material:material:1.6.1'
}
